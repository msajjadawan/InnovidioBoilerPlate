// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    // Kotlin Language and Gradle version
    // https://blog.jetbrains.com/kotlin/category/releases/
    // https://github.com/JetBrains/kotlin/blob/master/ChangeLog.md
    ext.kotlin_version = '1.3.72'

    // Dependency Injection for Android Applications
    // https://dagger.dev/hilt/gradle-setup
    ext.hilt_version = '2.28-alpha'

    repositories {
        google() // Google's Maven repository
        jcenter() // Add the Bintray repository

        // Gradle Plugin for Ktlint
        // https://github.com/jlleitschuh/ktlint-gradle#ktlint-plugin
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files


        // https://mvnrepository.com/artifact/com.android.tools.build/gradle?repo=google
        classpath "com.android.tools.build:gradle:4.0.0"

        // https://kotlinlang.org/docs/reference/using-gradle.html
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // https://dagger.dev/hilt/
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"

        // The Google Services Gradle Plugin
        // https://developers.google.com/android/guides/google-services-plugin
        classpath 'com.google.gms:google-services:4.3.3'

        // Firebase Crashlytics Gradle plugin
        // https://firebase.google.com/docs/crashlytics/get-started?platform=android
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.2.0'

        // https://github.com/jlleitschuh/ktlint-gradle#supported-kotlin-plugins
        classpath "org.jlleitschuh.gradle:ktlint-gradle:9.2.1"
    }
}

allprojects {
    repositories {
        google() // Google's Maven repository
        jcenter() // Add the Bintray repository
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}



// Define versions in a single place
ext {
    // SDK Level and Tools
    // https://source.android.com/setup/start/build-numbers
    // -----------------------------------------------------
    androidMinSdkVersion = 22 // Lollipop 5.1
    androidTargetSdkVersion = 29 // Android 10
    androidCompileSdkVersion = 29
    buildToolsVersion = '29.0.0' // Android SDK Build Tools - Versions: https://developer.android.com/studio/releases/build-tools.html

    // Google Products & Support library dependencies
    // AndroidX Versions: https://developer.android.com/jetpack/androidx/versions
    // --------------------------------------------------
    appCompatVersion = '1.1.0' // https://developer.android.com/jetpack/androidx/releases/appcompat
    constraintLayoutVersion = '1.1.3' // https://developer.android.com/reference/android/support/constraint/ConstraintLayout
    archComponentVersion = '2.1.0' // https://developer.android.com/topic/libraries/architecture/adding-components
    lifecycleVersion = '2.2.0' // https://developer.android.com/jetpack/androidx/releases/lifecycle
    firebaseVersion = '17.4.3' // https://firebase.google.com/docs/android/setup
    firebaseAnalytics = '17.4.3' // https://firebase.google.com/docs/analytics/get-started?platform=android
    crashlyticsVersion = '17.1.0' // https://firebase.google.com/docs/crashlytics/get-started?platform=android
    coreKtxVersion = '1.3.0' // https://developer.android.com/jetpack/androidx/releases/core
    androidMaterialVersion = '1.2.0-beta01' // https://github.com/material-components/material-components-android/releases
    viewPager2Version = '1.0.0' // https://developer.android.com/jetpack/androidx/releases/viewpager2
    androidXHilt = '1.0.0-alpha01' // https://developer.android.com/jetpack/androidx/releases/hilt

    // 3rd party library dependencies
    // --------------------------------------------------
    daggerVersion = '2.22.1' // https://github.com/google/dagger
    timberLibraryVersion = '4.7.1' // https://github.com/JakeWharton/timber
    leakcanaryLibraryVersion = '2.4' // https://github.com/square/leakcanary/releases
    rxJavaVersion = '2.2.9' // https://github.com/ReactiveX/RxJava/releases
    rxAndroidVersion = '2.1.1' // https://github.com/ReactiveX/RxAndroid/releases
    retrofitVersion = '2.9.0' // https://square.github.io/retrofit/
    gsonVersion = '2.8.6' // https://github.com/google/gson
    okHttpVersion = '4.7.2' // https://github.com/square/okhttp
    daggerVersion = '2.16' // https://github.com/google/dagger

    // Unit test dependencies
    // https://developer.android.com/training/testing/unit-testing/local-unit-tests
    // --------------------------------------------------
    junitVersion = '4.13'
    mockitoVersion = '3.3.3'
    androidxTestCore = '1.2.0'
    androidxArchCoreTesting = archComponentVersion
    androidTestRunnerVersion = '1.2.0'
    espressoVersion = '3.2.0'
}


//
// Utility methods used in the build
//

static def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}